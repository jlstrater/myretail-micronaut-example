plugins {
    id "groovy"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id "codenarc"
    id "jacoco"
}

version "0.1"
group "myretail.micronaut.example"
mainClassName = "myretail.micronaut.example.Application"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly 
}

dependencies {
    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor("io.micronaut:micronaut-management:$micronautVersion")
    compileOnly "io.micronaut.configuration:micronaut-openapi:1.5.0"
    compileOnly platform("io.micronaut:micronaut-bom:$micronautVersion")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-runtime-groovy"
    implementation "io.micronaut:micronaut-validation"
    implementation "javax.annotation:javax.annotation-api"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-http-client"
    implementation("io.micronaut.configuration:micronaut-redis-lettuce")
    implementation "io.swagger.core.v3:swagger-annotations"
    implementation "io.micronaut:micronaut-management:$micronautVersion"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testCompileOnly platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation "com.github.kstyrc:embedded-redis:0.6"
    testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.26.3"
}

test.classpath += configurations.developmentOnly

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote', '--illegal-access=debug')
}

codenarcMain {
    configFile = file("$projectDir/config/codenarc/codenarcMain.groovy")
}

codenarcTest {
    configFile = file("$projectDir/config/codenarc/codenarcTest.groovy")
}

compileGroovy {
    groovyOptions.configurationScript = file("$projectDir/config/groovyc/groovycConfig.groovy")
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
